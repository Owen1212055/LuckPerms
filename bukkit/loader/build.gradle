plugins {
    alias(libs.plugins.shadow)
}

sourceCompatibility = 21
targetCompatibility = 21

repositories {
    //maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven {
        url = uri("https://maven-prs.papermc.io/Paper/pr13201")
        name = "Maven for PR #13201" // https://github.com/PaperMC/Paper/pull/13201

        mavenContent {
            includeModule("io.papermc.paper", "dev-bundle")
            includeModule("io.papermc.paper", "paper-api")
        }
    }
}


dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'

    implementation project(':api')
    implementation project(':common:loader-utils')
}

processResources {
    filesMatching('plugin.yml') {
        expand 'pluginVersion': project.ext.fullVersion
    }
}

shadowJar {
    archiveFileName = "LuckPerms-Bukkit-${project.ext.fullVersion}.jar"

    from {
        project(':bukkit').tasks.shadowJar.archiveFile
    }

    manifest {
        attributes(["paperweight-mappings-namespace": "mojang"])
    }
}

artifacts {
    archives shadowJar
}
